#Использовать semver

&Пластилин
Перем КаталогиДанных;

Функция СпециальныеКаталоги()
	Имена = Новый Соответствие();
	Имена.Вставить("latest", Истина);
	Имена.Вставить("night-build", Истина);
	Имена.Вставить("latest-dev", Истина);
	Имена.Вставить("lts", Истина);
	Имена.Вставить("preview", Истина);

	Возврат Имена;
КонецФункции

Функция ТокеныВерсий(ФильтроватьСпециальные = Истина) Экспорт
	Папки = НайтиФайлы(КаталогиДанных.КаталогВерсий(), ПолучитьМаскуВсеФайлы(), Ложь);
	НеномерныеВерсии = СпециальныеКаталоги();

	ВременныйМассив = Новый Массив;
	Соответствие = Новый Соответствие;

	СпециальныеВерсии = Новый СписокЗначений;

	Для Каждого ФайлИлиПапка Из Папки Цикл

		ЭтоСпециальная = (НеномерныеВерсии[ФайлИлиПапка.Имя] <> Неопределено);

		Если ФайлИлиПапка.ЭтоКаталог() Тогда
			ИмяВерсии = ФайлИлиПапка.Имя;
			
			Если ЭтоСпециальная Тогда
				Если Не ФильтроватьСпециальные Тогда
					СпециальныеВерсии.Добавить(ИмяВерсии);
				КонецЕсли;
			Иначе
				ЗначениеВФорматеВерсии = СтрЗаменить(ИмяВерсии, "_", ".");
				ВременныйМассив.Добавить(ЗначениеВФорматеВерсии);
				Соответствие.Вставить(ЗначениеВФорматеВерсии, ИмяВерсии);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	СпециальныеВерсии.СортироватьПоЗначению();
	Версии.СортироватьВерсии(ВременныйМассив, "УБЫВ");
	
	ТаблицаВерсий = Новый ТаблицаЗначений();
	ТаблицаВерсий.Колонки.Добавить("Токен");
	ТаблицаВерсий.Колонки.Добавить("Представление");

	Для Каждого СпецВерсия Из СпециальныеВерсии Цикл
		ОписаниеВерсии = ТаблицаВерсий.Добавить();
		ОписаниеВерсии.Токен = СпецВерсия.Значение;
		ОписаниеВерсии.Представление = СпецВерсия.Значение;
	КонецЦикла;

	Для Каждого ЭлементМассива Из ВременныйМассив Цикл
		
		ЗначениеИзСоответствия = Соответствие.Получить(ЭлементМассива); 

		ОписаниеВерсии = ТаблицаВерсий.Добавить();
		ОписаниеВерсии.Токен = ЗначениеИзСоответствия;
		ОписаниеВерсии.Представление = ЭлементМассива;
		
	КонецЦикла;

	Возврат ТаблицаВерсий;

КонецФункции

Функция ФайлДокументации(ТокенВерсии) Экспорт
	КаталогОписанийВерсий = ОбъединитьПути(КаталогиДанных.КаталогДокументации(), "versions");
	
	ФайлОписания = Новый Файл(ОбъединитьПути(КаталогОписанийВерсий, ТокенВерсии + ".md"));
	Если Не ФайлОписания.Существует() или Не СтрНачинаетсяС(ФайлОписания.ПолноеИмя, КаталогОписанийВерсий) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат ФайлОписания;

КонецФункции

&Желудь
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры