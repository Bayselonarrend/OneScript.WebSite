&Пластилин
Перем АрхивВерсий;

&Пластилин
Перем ФабрикаВерсий;

&Желудь
&Прозвище("Обработчик")
&Порядок(2)
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Процедура Обработать(Знач Контекст, Знач СледующийОбработчик) Экспорт
	Сегменты = Контекст.Данные["api"];
	Если Сегменты = Неопределено Тогда
		СледующийОбработчик.Вызвать(Контекст);
		Возврат;
	КонецЕсли;

	Если Сегменты[1] = "archive" Тогда
		Если Не ОбработатьАрхив(Контекст, Сегменты) Тогда
			СледующийОбработчик.Вызвать(Контекст);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ОбработатьАрхив(Контекст, Сегменты)
	Если Сегменты.Количество() = 2 Тогда // api/archive
		Возврат СписокНомерныхВерсий(Контекст);
	ИначеЕсли Сегменты.Количество() = 3 Тогда // api/archive/$version
		Возврат ФайлыВерсии(Контекст, Сегменты[2]);
	ИначеЕсли Сегменты.Количество() = 4 Тогда // api/archive/$version/$file
		// это файл md
	КонецЕсли;

	Возврат Ложь;
КонецФункции

Функция СписокНомерныхВерсий(Контекст)
	МассивТокенов = АрхивВерсий.ТокеныВерсий();

	Ответ = Контекст.Ответ;
	
	Ответ.КодСостояния = 200;
	Ответ.ТипКонтента = "application/json";
	Ответ.ЗаписатьКакJson(МассивТокенов);

	Возврат Истина;

КонецФункции

Функция ФайлыВерсии(Контекст, ТокенВерсии)

	Состав = ФабрикаВерсий.ПолучитьСоставВерсии(ТокенВерсии).ПолучитьСостав();
	Если Не ЗначениеЗаполнено(Состав) Тогда
		Ответ = Контекст.Ответ;
	
		Ответ.КодСостояния = 200;
		Ответ.ТипКонтента = "text/plain";
		Ответ.Записать("Not found");

		Возврат Истина;

	КонецЕсли;

	Ответ = Контекст.Ответ;
	Ответ.КодСостояния = 200;
	Ответ.ТипКонтента = "application/json";

	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();

	ЗаписьJSON.ЗаписатьНачалоМассива();
	Для Каждого ОписаниеФайла Из Состав Цикл
		ЗаписьJSON.ЗаписатьНачалоОбъекта();

		ЗаписатьСвойство(ЗаписьJSON, "id", ОписаниеФайла.Идентификатор);
		ЗаписатьСвойство(ЗаписьJSON, "filename", ОписаниеФайла.ИмяФайла);
		ЗаписатьСвойство(ЗаписьJSON, "arch", ОписаниеФайла.Архитектура);
		ЗаписатьСвойство(ЗаписьJSON, "dist-type", ОписаниеФайла.ВидДистрибутива);
		ЗаписатьСвойство(ЗаписьJSON, "modified", ОписаниеФайла.ДатаФайла);
		ЗаписатьСвойство(ЗаписьJSON, "link", ОписаниеФайла.Ссылка);

		ЗаписьJSON.ЗаписатьКонецОбъекта();
	КонецЦикла;

	ЗаписьJSON.ЗаписатьКонецМассива();

	Ответ.Записать(ЗаписьJSON.Закрыть());

	Возврат Истина;

КонецФункции

Процедура ЗаписатьСвойство(ЗаписьJSON, ИмяСвойства, Значение)
	ЗаписьJSON.ЗаписатьИмяСвойства(ИмяСвойства);
	ЗаписьJSON.ЗаписатьЗначение(Значение);
КонецПроцедуры