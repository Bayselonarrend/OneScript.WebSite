&Пластилин
Перем АрхивВерсий;

&Пластилин
Перем ФабрикаВерсий;

&Желудь
&Прозвище("Обработчик")
&Порядок(2)
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Процедура Обработать(Знач Контекст, Знач СледующийОбработчик) Экспорт
	Сегменты = Контекст.Данные["api"];
	Если Сегменты = Неопределено Тогда
		СледующийОбработчик.Вызвать(Контекст);
		Возврат;
	КонецЕсли;

	Если Сегменты[1] = "archive" Тогда
		ОбработатьАрхив(Контекст, Сегменты);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьАрхив(Контекст, Сегменты)
	Если Сегменты.Количество() = 2 Тогда // api/archive
		СписокНомерныхВерсий(Контекст);
	ИначеЕсли Сегменты.Количество() = 3 Тогда // api/archive/$version
		ФайлыВерсии(Контекст, Сегменты[2]);
	ИначеЕсли Сегменты.Количество() = 4 и Сегменты[3] = "md" Тогда // api/archive/$version/md
		ФайлДокументации(Контекст, Сегменты[2]);
	Иначе
		ОтветыСервера.Отдать404(Контекст.Ответ);
	КонецЕсли;
КонецПроцедуры

Процедура СписокНомерныхВерсий(Контекст)
	МассивТокенов = АрхивВерсий.ТокеныВерсий();

	Ответ = Контекст.Ответ;
	
	Ответ.КодСостояния = 200;
	Ответ.ТипКонтента = "application/json";
	Ответ.ЗаписатьКакJson(МассивТокенов);

КонецПроцедуры

Процедура ФайлыВерсии(Контекст, ТокенВерсии)

	Состав = ФабрикаВерсий.ПолучитьСоставВерсии(ТокенВерсии).ПолучитьСостав();
	Если Не ЗначениеЗаполнено(Состав) Тогда
		ОтветыСервера.Отдать404(Контекст.Ответ);
		Возврат;
	КонецЕсли;

	Ответ = Контекст.Ответ;
	Ответ.КодСостояния = 200;
	Ответ.ТипКонтента = "application/json";

	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();

	ЗаписьJSON.ЗаписатьНачалоМассива();
	Для Каждого ОписаниеФайла Из Состав Цикл
		ЗаписьJSON.ЗаписатьНачалоОбъекта();

		ЗаписатьСвойство(ЗаписьJSON, "id", ОписаниеФайла.Идентификатор);
		ЗаписатьСвойство(ЗаписьJSON, "filename", ОписаниеФайла.ИмяФайла);
		ЗаписатьСвойство(ЗаписьJSON, "arch", ОписаниеФайла.Архитектура);
		ЗаписатьСвойство(ЗаписьJSON, "distType", ОписаниеФайла.ВидДистрибутива);
		ЗаписатьСвойство(ЗаписьJSON, "modified", ОписаниеФайла.ДатаФайла);
		ЗаписатьСвойство(ЗаписьJSON, "link", ОписаниеФайла.Ссылка);

		ЗаписьJSON.ЗаписатьКонецОбъекта();
	КонецЦикла;

	ЗаписьJSON.ЗаписатьКонецМассива();

	Ответ.Записать(ЗаписьJSON.Закрыть());
КонецПроцедуры

Процедура ЗаписатьСвойство(ЗаписьJSON, ИмяСвойства, Значение)
	ЗаписьJSON.ЗаписатьИмяСвойства(ИмяСвойства);
	ЗаписьJSON.ЗаписатьЗначение(Значение);
КонецПроцедуры

Процедура ФайлДокументации(Контекст, ТокенВерсии)
	ФайлОписания = АрхивВерсий.ФайлДокументации(ТокенВерсии);
	Если ФайлОписания = Неопределено Тогда
		ОтветыСервера.Отдать404(Контекст.Ответ);
	Иначе
		ОтветыСервера.ОтдатьФайл(Контекст.Ответ, ФайлОписания, "text/plain");
	КонецЕсли;
КонецПроцедуры