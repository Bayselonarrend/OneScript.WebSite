&Пластилин
Перем КаталогиДанных;

&Желудь
&Прозвище("Обработчик")
&Порядок(3)
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Процедура Обработать(Знач Контекст, Знач СледующийОбработчик) Экспорт
	
	Перем ФайлЗапроса;
	Перем ПутьЗапроса;

	Если СтрНачинаетсяС(Контекст.Запрос.Путь, "/") Тогда
		ПутьЗапроса = Сред(Контекст.Запрос.Путь, 2);
	Иначе
		ПутьЗапроса = Контекст.Запрос.Путь;
	КонецЕсли;

	Попытка
		ФайлЗапроса = Новый Файл(ОбъединитьПути(КаталогиДанных.КаталогФронтенда(), ПутьЗапроса));
	Исключение
		СледующийОбработчик.Вызвать(Контекст);
		Возврат;
	КонецПопытки;

	Если ФайлЗапроса.Существует() и Не СтрНачинаетсяС(ФайлЗапроса.ПолноеИмя, КаталогиДанных.КаталогФронтенда()) Тогда
		ВызватьИсключение "Путь ведет наружу из каталога фронта";
	КонецЕсли;

	// Сюда попадем, если это запрос индекса или файла без расширения в подкаталоге
	// т.к. про реальному пути файл будет отдан миддлварем статик-файлов кестрела
	Если ФайлЗапроса.Существует() Тогда
		Если Не ФайлЗапроса.ЭтоКаталог() Тогда
			ВызватьИсключение "Подозрительный путь " + ФайлЗапроса.ПолноеИмя;
		КонецЕсли;

		ОтдатьИндекс(ФайлЗапроса, Контекст);
		Возврат;
	Иначе
		ФайлСтраницы = Новый Файл(ФайлЗапроса.ПолноеИмя + ".html");
		ОтдатьСуществующийФайл(ФайлСтраницы, Контекст);
		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура ОтдатьИндекс(Знач Каталог, Знач Контекст)
	ПутьИндекса = Новый Файл(ОбъединитьПути(Каталог.ПолноеИмя, "index.html"));
	ОтдатьСуществующийФайл(ПутьИндекса, Контекст);
КонецПроцедуры

Процедура ОтдатьСуществующийФайл(Знач Файл, Знач Контекст)
	Если Файл.Существует() Тогда
		ОтправитьФайл(Файл, Контекст.Ответ);
	Иначе
		ОтветыСервера.Отдать404(Контекст.Ответ);
	КонецЕсли;
КонецПроцедуры

Процедура ОтправитьФайл(Знач Файл, Знач Ответ)
	ОтветыСервера.ОтдатьФайл(Ответ, Файл, "text/html");
КонецПроцедуры