&Пластилин
Перем ФабрикаВерсий;

&Пластилин
Перем КаталогиДанных;

&Желудь
&Прозвище("Обработчик")
&Порядок(2)
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Процедура Обработать(Знач Контекст, Знач СледующийОбработчик) Экспорт
	Сегменты = Контекст.Данные["downloads"];
	Если Сегменты = Неопределено Тогда
		СледующийОбработчик.Вызвать(Контекст);
		Возврат;
	КонецЕсли;

	// Сначала пробуем взять по пути
	ЭлементыПути = Новый Массив;
	Для Сч = 1 По Сегменты.ВГраница() Цикл
		ЭлементыПути.Добавить(Сегменты[Сч]);
	КонецЦикла;

	ВозможныйПуть = СтрСоединить(ЭлементыПути, ПолучитьРазделительПути());
	ПутьФайла = ОбъединитьПути(КаталогиДанных.КаталогВерсий(), ВозможныйПуть);
	Файл = Новый Файл(ПутьФайла);
	Если Файл.Существует() И Файл.ЭтоФайл() И СтрНачинаетсяС(Файл.ПолноеИмя, КаталогиДанных.КаталогВерсий()) Тогда
		
		ОтправитьФайл(Файл, Контекст.Ответ);
		Возврат;

	КонецЕсли;

	// Значит это мета-путь без имени файла
	ТокенВерсии = Сегмент(Сегменты, 1);
	ВидДистрибутива = Сегмент(Сегменты, 2);
	Разрядность = НеобязательныйСегмент(Сегменты, 3);
	
	Если ПустаяСтрока(Разрядность) Тогда
		// Посмотрим в параметр bitness
		РазрядностьВПараметре = Контекст.Запрос.Параметры.Получить("bitness");
		Если Не РазрядностьВПараметре = Неопределено Тогда
			Разрядность = РазрядностьВПараметре;
		КонецЕсли;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Разрядность) Тогда
		Разрядность = "x86";
	КонецЕсли;

	СоставВерсии = ФабрикаВерсий.ПолучитьСоставВерсии(ТокенВерсии);
	Сборка = СоставВерсии.НайтиВариантСборки(ВидДистрибутива, Разрядность);
	Если Сборка = Неопределено Тогда
		Ответ = Контекст.Ответ;
		Ответ.КодСостояния = 404;
		Возврат;
	КонецЕсли;

	ОтправитьФайл(Сборка.Файл, Контекст.Ответ);

КонецПроцедуры

Процедура ОтправитьФайл(Знач Файл, Знач Ответ)
	Ответ.КодСостояния = 200;
	Ответ.Заголовки["Content-Disposition"] = "attachment; filename=" + Файл.Имя;
	
	ОтветыСервера.ОтдатьФайл(Ответ, Файл, "application/octet-stream");
КонецПроцедуры

Функция Сегмент(Сегменты, Номер)
	Возврат Сегменты[Номер];
КонецФункции

Функция НеобязательныйСегмент(Сегменты, Номер)
	Если Номер < Сегменты.Количество() Тогда
		Возврат Сегменты[Номер];
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции